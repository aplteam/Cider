  no←{alias}GetNumberOfCommits(commits branch folder caption);flag;b;buff;width;sep;q
⍝ Asks the user for the number of commits to be processed.
⍝ The optional left argument can be used to defien an alias for the CommTools.
⍝ This can be used to automate the test case.
 alias←{0<⎕NC ⍵:⍎⍵ ⋄ ''}'alias'
 width←⎕PW
 sep←⎕UCS 13
 q←width↑('--- Commits in branch "',branch,'" of project "',folder,'"'),' ',⎕PW⍴'-'
 buff←(↑commits[;1]),' ',(↑commits[;2]),' ',(↑commits[;3]),' ',↑commits[;4]
 buff←A.DTB⍕¨(((0,⍨≢⍕≢buff)∘⍕¨⍳≢buff),¨⊂'. '),¨↓buff
 q←q,sep,(⊃,/buff,¨sep),sep,caption
 no←((≢commits)∘{⍵∊⍳⍺}##.CommTools.AskForNumber)alias,q ⍝ caption
 :If 0=≢no
     no←0
 :EndIf
