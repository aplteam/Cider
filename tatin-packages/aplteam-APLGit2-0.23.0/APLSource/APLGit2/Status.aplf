 r←{short}Status y;rc;more;path;⎕TRAP;short;space;ind;branch;msg;cmd
⍝ Reports all unstaged/uncommited/untracked files.\\
⍝ `y` must be one of:
⍝   * Fully qualfied name of a namespace that is an opened Cider project
⍝   * Fully qualified path to a Git-managed project
⍝ `short` defaults to 0
⍝ * short←0 gives a full report whith headers, usually useful only for the user command
⍝ * short←1 returns a matrix with 2 columns: [;1] indicators, [;2] the path
 ⎕TRAP←(811 'E' '⎕DMX.EM ⎕signal 811')(0 'N')
 short←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'short'
 (space path)←EstablishProject y
 cmd←'status',short/' --short'
 (rc more r)←path U.RunGitCommand cmd
 more U.Assert 0=rc
 ((≢'fatal:')↓more)U.Assert'fatal:'{⍺≢(≢⍺)↑⍵}more
 :If short
     r←(2↑¨r),[1.5]3↓¨r
 :Else
     ind←⍸∨/¨'On branch'∘⍷¨r
     branch←⊃¯1↑' '(≠⊆⊢)ind⊃r
     :If (⊂branch)∊'main' 'master'
         msg←∊(⎕UCS 13),U.Box{⍵{(⍵⍴'⌹')⍺(⍵⍴'⌹')}≢⍵}'⌹ On branch ',branch,' ⌹'
     :Else
         msg←'On branch ',branch
     :EndIf
     (ind⊃r)←msg
     r←(⊂path{0<≢⍵:'Namespace: ',⍵ ⋄ 'Folder: ',⍺}space),⊆r
 :EndIf
⍝Done
