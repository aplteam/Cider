 {r}←OpenGitShell y;bool;cmd;home;path;pathToGitExe;paths;space
⍝ Opens a Terminal in `y`.\\
⍝ `y` must be one of:\\
⍝   * Fully qualified path to a Git-managed project
⍝   * Fully qualified name of a namespace that is an opened Cider project\\
⍝   * A dot (`.`) for "current directory"
⍝ Returns always 0\\
⍝ This has been tested on Windows, Mac OS and Ubuntu.
 r←0
 :If (,'.')≡,y
     path←'.'
     space←''
 :Else
     (space path)←EstablishProject y
 :EndIf
 :Select ⎕C A.GetOperatingSystem ⍬
 :Case 'win'
     paths←';'A.Split 2 ⎕NQ #'GetEnvironment' 'PATH'
     :If 1=+/bool←∨/¨'\git\bin'∘⍷¨⎕C paths
         home←(bool⍳1)⊃paths
         pathToGitExe←home,'\sh.exe'
     :Else
         'GitBash not found!'⎕SIGNAL 6
     :EndIf
     cmd←('"',pathToGitExe,'" --cd="',path,'"')
     ⎕SH cmd''
 :Case 'lin'
     cmd←'gnome-terminal -- $SHELL -c "cd ',path,';git status; exec $SHELL"'
     ⎕SH cmd
 :Case 'mac'
     cmd←'osascript -e ''tell app "terminal" to do script "cd ',path,';git status "'''
     ⎕SH cmd
 :EndSelect
⍝Done
