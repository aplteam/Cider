 R←Test_007(stopFlag batchFlag);A;buffer;⎕TRAP;noOf;result
⍝ Exercise "Lowercase"
 A←##.APLTreeUtils2
 R←T._Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 →T.FailsIf''≢buffer←A.Lowercase''
 →T.FailsIf'kai jaeger apl software team'≢buffer←A.Lowercase'KAI JAEGER APL Software Team'
 →T.FailsIf'kai jaeger' 'apl software team'≢buffer←A.Lowercase'KAI JAEGER' 'APL Software Team'
 →T.FailsIf'üäößüäö'≢buffer←A.Lowercase'üäößÜÄÖ'
 →T.FailsIf'ü'≢buffer←A.Lowercase'ü'
 →T.FailsIf(,'ü')≢buffer←A.Lowercase,'ü'
 →T.FailsIf'ü'≢buffer←A.Lowercase'Ü'
 →T.FailsIf(,'ü')≢buffer←A.Lowercase,'Ü'
 →T.FailsIf'a'≢buffer←A.Lowercase'A'
 →T.FailsIf(,'a')≢buffer←A.Lowercase,'A'
 →T.FailsIf'∑x²'≢buffer←A.Lowercase'∑x²'
 →T.FailsIf(2 2⍴'aabb')≢buffer←A.Lowercase 2 2⍴'AaBb'

 :If 15>⊃(//)⎕VFI⊃'.'A.Split(1+⎕IO)⊃'#'⎕WG' APLVersion'
     →T.FailsIf'' '' '' ''≢,buffer←A.Lowercase''⍬''⍬
 :EndIf

 noOf←⌊0.5×⌊⎕WA÷1000
 buffer←(⊂1000000⍴'APL IS FINE '),'A',(noOf)⍴⊂'ABC'   ⍝ This will cause a WS FULL when converted into a matrix
 result←A.Lowercase buffer
 buffer←(⊂1000000⍴'apl is fine '),'a',(noOf⍴⊂'abc')
 →T.FailsIf buffer≢result

 R←T._OK
