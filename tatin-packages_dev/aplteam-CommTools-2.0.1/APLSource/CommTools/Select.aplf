 index←{x}Select choices;flag;answer;question;value;bool;⎕ML;⎕IO;manyFlag;mustFlag;caption;buff;flag2;alias;blankFlag;firstPart;Select_Choices
⍝ Presents `choices` as a numbered list and allows the user to select either exactly one or multiple ones.
⍝ Selecting just one is the default.\\
⍝ The optional left argument allows you to specify multiple (positional) choices:
⍝  * `caption` is shown above the choices; must be a simple character vector
⍝  * `manyFlag` defaults to 0 (= just 1 item might be selected) or 1 (allow selecting multiple items)
⍝  * `mustFlag` forces the user to select at least one option, disallowing to quit without making a decision\\
⍝ Notes:
⍝  * `choices` must not have more than 999 items\\
⍝  * By entering "`∘∘∘`" the user may interrupt `Select` by running onto a stop vector, overcoming
⍝    the Dyalog bug that ⍞ cannot be interrupted
⍝  * You can automate `Select`, refer to the documentation with ]ADoc CommTools, catch "Automation"\\
⍝  * If the user aborts by entering "q" (for "quit") `⍬` will be returned
 x←{0<⎕NC ⍵:⊆⍎⍵ ⋄ ''}'x'
 (caption manyFlag mustFlag)←x,(⍴,x)↓'' 0 0
 '"caption" must be a simple character vector'Assert 1=≡caption←,caption
 '"caption" must not contain line feeds (⎕UCS 10)'Assert~LF∊caption
 '"caption" must not contain carriage returns (⎕UCS 13)'Assert~(⎕UCS 13)∊caption
 ⎕IO←1 ⋄ ⎕ML←1
 'Invalid right argument; must be a vector of text vectors.'⎕SIGNAL ErrNo/⍨2≠|≡choices
 'Right argument has more than 999 items'⎕SIGNAL ErrNo/⍨999<≢choices
 flag←0
 firstPart←{⍵↑⍨⍵⍳LF}caption
 :If 0<⎕NC'∆Automation'
 :AndIf 0<≢∆Automation
     Select_Choices←(∆Automation[;1]≡¨⊂'Select')⌿∆Automation[;2 3]
 :AndIf 0<≢Select_Choices
     :If AliasChar∊caption
         (alias caption)←{l←⍵⍳AliasChar ⋄ (l↑⍵)(l↓⍵)}caption
         flag←0<+/bool←({⍵↑⍨(,⍵)⍳AliasChar}¨Select_Choices[;1])≡¨⊂alias
     :Else
         bool←caption∘≡¨Select_Choices[;1]
         flag←0<+/bool
     :EndIf
     :If flag
         'Multiple choices qualify?!'Assert 1=+/bool
         index←2⊃Select_Choices[bool⍳1;]
         'Select'Log⊃Select_Choices[bool⍳1;]
         :If (⊂index)∊0 ⍬
             index←⍬
         :ElseIf ' '=1↑0⍴∊index ⍝ Text?!
             :If '*'=¯1↑index
                 index←⍸((¯1+≢index)↑¨{⍵↓⍨+/∧\' '=⍵}¨choices)≡¨⊂¯1↓index
                 index←{1=≢⍵:⊃⍵ ⋄ ⍵}index
                 'Invalid selection'Assert(mustFlag{(0<≢⍵)≠⍺}index)∨∨/index∊⍳≢choices
             :ElseIf '*'=⊃index
                 index←⍸((-¯1+≢index)↑¨{⍵↓⍨-+/∧\' '=⌽⍵}¨choices)≡¨⊂1↓index
                 index←{1=≢⍵:⊃⍵ ⋄ ⍵}index
                 'Invalid selection'Assert(mustFlag{(0<≢⍵)≠⍺}index)∨∨/index∊⍳≢choices
             :ElseIf (,'a')≡,index
                 index←⍳≢choices
             :ElseIf (,'q')≡,index
                 index←⍬
             :ElseIf 0<≢buff←⍸index∘≡¨choices
                 index←buff
                 'Invalid selection'Assert index∊⍳≢choices
             :ElseIf 1=≢index←⍸index∘≡¨(≢index)↑¨{⍵↓⍨+/∧\' '=⍵}¨choices
                 index←⊃index
                 'Invalid selection'Assert index∊⍳≢choices
             :Else
                 'Invalid selection'Assert 0
             :EndIf
         :Else
             'Invalid selection'Assert∧/index∊⍳≢choices
         :EndIf
     :EndIf
 :EndIf
 :If ~flag
     flag2←0
     :Repeat
         blankFlag←0≠≢caption
         :If AliasChar∊caption
             :If (firstPart⍳' ')>firstPart⍳AliasChar
                 caption←{~AliasChar∊⍵:⍵ ⋄ ⍵↓⍨⍵⍳AliasChar}caption
             :EndIf
         :EndIf
         ⎕←{⍵↑'---',(blankFlag/' '),caption,(blankFlag/' '),⍵⍴'-'}⎕PW-1
         ⎕←⍪{((⊂'. '),¨⍨(⊂3 0)⍕¨⍳≢⍵),¨⍵}choices
         ⎕←''
         question←'Select one ',(manyFlag/'or more '),'item',((manyFlag)/'s'),' '
         question,←((manyFlag∨~mustFlag)/'('),((~mustFlag)/'q=quit'),((manyFlag∧~mustFlag)/', '),(manyFlag/'a=all'),((manyFlag∨~mustFlag)/')'),' :'
         :If 0<≢answer←⍞,0/⍞←question
             answer←{⍵↑⍨-¯1+(⌽⍵)⍳':'}answer
             :If '∘∘∘'≡¯3↑answer
                 (1+⊃⎕LC)⎕STOP⊃⎕SI
                 ∘∘∘ ⍝ Deliberate stop caused by user input
             :EndIf
             :If 1=≢answer
             :AndIf answer∊'Qq',manyFlag/'Aa'
                 :If answer∊'Qq'
                     :If 0=mustFlag
                         index←⍬
                         flag2←1
                     :EndIf
                 :Else
                     index←⍳≢choices
                     flag2←1
                 :EndIf
             :Else
                 (bool value)←⎕VFI answer
                 :If ∧/bool
                 :AndIf manyFlag∨1=+/bool
                     value←bool/value
                 :AndIf ∧/value∊⍳⍴choices
                     index←value
                     flag2←0≠≢index
                 :EndIf
             :EndIf
         :EndIf
     :Until flag2
     index←{1<≢⍵:⍵ ⋄ ⊃⍵}⍣(⍬≢index)⊣index
 :EndIf
⍝Done
