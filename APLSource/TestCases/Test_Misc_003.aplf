 R←Test_Misc_003(stopFlag batchFlag);⎕TRAP;folder;parms;list;filename;cfg;was;rc;success;log;res
⍝ Exercise the OpenProject function with ExecuteAfterProjectOpen
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 'TEMP2' 'TEMP'#.⎕NS¨⊂''
 #.TEMP2.∆FLAG←0

 folder←∆GetTestPath⊃⎕SI

 filename←##.Cider.GetCiderGlobalConfigFilename
 cfg←⎕JSON⍠('Dialect' 'JSON5')⊣⊃⎕NGET filename

 was←cfg.{0=⎕NC'ExecuteAfterProjectOpen':'' ⋄ ExecuteAfterProjectOpen}⍬
 cfg.ExecuteAfterProjectOpen←'#.TEMP2.ExecuteAfterProjectOpen'
 #.TEMP2.⎕FX'ExecuteAfterProjectOpen config' '∆FLAG←1'
 (⊂⎕JSON⍠('Compact' 0)('Dialect' 'JSON5')⊣cfg)⎕NPUT filename 1

 parms←⎕NS''
 parms.folder←folder,'/Project1'
 parms.batch←1
 ##.Cider.C.AddAutomation'YesOrNo' 'ChangeCurrentDir@' 'n'
 (success log)←##.Cider.OpenProject parms
 →T.GoToTidyUp~success
 →T.GoToTidyUp∨/'ERROR'⍷log
 →T.GoToTidyUp 1≠#.TEMP2.∆FLAG

 R←T._OK

∆TidyUp:
 ##.Cider.C.Cleanup
 cfg.ExecuteAfterProjectOpen←was
 (⊂⎕JSON⍠('Dialect' 'JSON5')('Compact' 0)⊣cfg)⎕NPUT filename 1
 res←∆LINK'Break' '#.TEMP'
 Assert res ∆StartsWith'Unlinked:'
 #.⎕EX¨'TEMP' 'TEMP2'
⍝Done
