 R←Test_Open_051(stopFlag batchFlag);⎕TRAP;parms;body1;body2;filename;body3;success;log
⍝ Open a project with watch=both
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 :If 1 1≢2250⌶0    ⍝ We need .NET Core and a successfully loaded .NET bridge
     R←T._NotApplicable ⋄ →0
 :EndIf

 'TEMP'#.⎕NS''

 parms←##.Cider.CreateOpenParms ⍬
 parms.folder←∆GetTestPath⊃⎕SI
 parms.parent←'#.TEMP'
 parms.watch←'both'
 parms.ignoreUserExec←1
 parms.checkPackageVersions←2

 filename←parms.folder,'/APLSource/Hello.aplf'
 (⊂'r←Hello' 'r←''World''')⎕NPUT filename 1
 ##.Cider.C.YesOrNo_Answers←1 2⍴'ChangeCurrentDir@' 'n'

 (success log)←##.Cider.OpenProject parms
 Assert success
 →T.GoToTidyUp∨/'ERROR'⍷log

 body1←⊃⎕NGET filename
 Assert(,⊂'Hello')≡#.TEMP.Foo ⎕SE.Link.Fix'r←Hello' 'r←''Galaxy'''
 →T.GoToTidyUp'Galaxy'≢#.TEMP.Foo.Hello
 body2←⊃⎕NGET filename
 →T.GoToTidyUp body1≡body2

 (⊂'r←Hello' 'r←''Universe''')⎕NPUT filename 1
 ⎕DL 0.3
 body3←⊃⎕NGET filename
 →T.GoToTidyUp body2≡body3

 R←T._OK

∆TidyUp:
 ##.Cider.C.Cleanup
 {}0 ⎕SE.Cider.CloseProject'#.TEMP.Foo'
 (⊂'r←Hello' 'r←''World''')⎕NPUT filename 1
 #.⎕EX'TEMP'
⍝Done
