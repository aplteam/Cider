 R←Test_Open_020(stopFlag batchFlag);⎕TRAP;parms;r;success;log
⍝ Open a project and specify "parent" via parameter
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 'TEMP'#.⎕NS''

 parms←##.Cider.CreateOpenParms ⍬
 parms.folder←∆GetTestPath⊃⎕SI
 parms.parent←'#.TEMP'
 parms.importFlag←1
 parms.watch←'ns'
 parms.ignoreUserExec←1
 parms.batch←1
 ##.Cider.C.YesOrNo_Answers←1 2⍴'ChangeCurrentDir@' 'n'
 (success log)←##.Cider.OpenProject parms
 →T.GoToTidyUp~success
 →T.GoToTidyUp∨/'ERROR'⍷log
 →T.GoToTidyUp'World'≢#.TEMP.Foo.Hello

 R←T._OK

∆TidyUp:
 ##.Cider.C.Cleanup
 #.⎕EX'TEMP'
⍝Done
