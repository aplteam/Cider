 R←Test_Create_001(stopFlag batchFlag);⎕TRAP;tempDir;list;res;stop;msg;rc;ref
⍝ Attempts to create and then open a project with an empty namespace and a non-empty folder with
⍝ A config file is copied over in order to avoid user interaction
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕EX'Foo'
 tempDir←∆GetFolderInTempDir⊃⎕SI
 3 ⎕MKDIR tempDir,'/APLSource'
 (⊂'r←TheAnswerIs' 'r←42')⎕NPUT(tempDir,'/APLSource/TheAnswerIs.aplf')1
 (tempDir,'/cider.config')⎕NCOPY(##.CiderConfig.HOME,'/TestData/cider.config.Foo')

 ref←∆GetRefToCommTools
 ref.AddAutomation'YesOrNo' 'ChangeCurrentDir@' 'n'
 stop←0
 (rc msg res)←stop ∆UCMD'CreateProject "',tempDir,'" -acceptConfig -batch -noEdit -ignoreUserExec'
 Assert 0=rc
 Assert 0=≢msg
 →T.GoToTidyUp 2≠≢#.Foo.⎕NL⍳16  ⍝ TheAnswerIs plus CiderConfig = 2
 →T.GoToTidyUp 1≠≢⊃FilesAndDirs.Dir tempDir,'/APLSource/'

 R←T._OK

∆TidyUp:
 ref.Cleanup
 {}⎕SE.Link.Break'#.Foo'
 rc←⊃FilesAndDirs.RmDir tempDir,'/..'
 Assert rc∧.=0
 #.⎕EX'Foo'
⍝Done
