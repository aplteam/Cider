 R←∆Test_NuGet_004(stopFlag batchFlag);⎕TRAP;folder;parms;res;log;cals;success;tempDir;config;list
⍝ Open a project, add two NuGet dependencies with a target namespace, load, call, list, close, re-open, close
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS''
 folder←∆GetTestPath⊃⎕SI
 tempDir←∆TEMP_PATH
 tempDir←∆TranslateAlias tempDir
 tempDir ⎕NCOPY⍠1⊢folder,'/*'

 config←##.Cider.ReadProjectConfigFile tempDir
 config.CIDER.dependencies.nuget←'MyNugetPkgs=Utils'
 config ##.Cider.WriteProjectConfigFile tempDir

 parms←##.Cider.CreateOpenParms''
 parms.folder←tempDir
 parms.noPkgLoad←0
 parms.batch←1
 parms.ignoreUserExec←1

 ##.Cider.C.AddAutomation'YesOrNo' 'ChangeCurrentDir@' 'n'
 (success log)←##.Cider.OpenProject parms
 →T.GoToTidyUp~success
 →T.GoToTidyUp∨/'ERROR'⍷log

 list←##.Cider.AddNuGetDependencies'Clock,NodaTime'tempDir
 →T.GoToTidyUp~'Clock' 'NodaTime'∆LazyMatch list

 list←0 1 ##.Cider.LoadNuGetDependencies tempDir #.TEMP
 →T.GoToTidyUp~'Clock' 'NodaTime'∆LazyMatch list

 →T.GoToTidyUp~(⊂'UtcNow')∊#.TEMP.Utils.Clock.⎕NL-2
 →T.GoToTidyUp~∧/'Julian' 'Gregorian'∊⍕¨#.TEMP.Utils.NodaTime.CalendarSystem.⎕NL-2

 res←##.Cider.ListNuGetDependencies tempDir
 →T.GoToTidyUp 2≠≡res
 →T.GoToTidyUp~'Clock' 'NodaTime'∆LazyMatch res[;1]

 res←(~parms.batch)##.Cider.CloseProject tempDir
 →T.GoToTidyUp res≠1

 #.TEMP.(⎕EX ⎕NL-⍳9)
 (success log)←##.Cider.OpenProject parms
 →T.GoToTidyUp~success
 →T.GoToTidyUp∨/'ERROR'⍷log
 →T.GoToTidyUp~(⊂'UtcNow')∊#.TEMP.Utils.Clock.⎕NL-2
 →T.GoToTidyUp~∧/'Julian' 'Gregorian'∊⍕¨#.TEMP.Utils.NodaTime.CalendarSystem.⎕NL-2

 res←(~parms.batch)##.Cider.CloseProject tempDir
 →T.GoToTidyUp res≠1

 R←T._OK

∆TidyUp:
 ##.Cider.C.Cleanup
 res←∆LINK'Break' '#.TEMP'
⍝Done
