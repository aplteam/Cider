 R←∆Test_NuGet_020(stopFlag batchFlag);⎕TRAP;folder;tempDir;config;parms;success;log;qdmx;expected;res
⍝ Open a project and add some NuGet dependencies wrongly separated by a space
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS''
 folder←∆GetTestPath⊃⎕SI
 tempDir←∆GetFolderInTempDir⊃⎕SI
 tempDir ⎕NCOPY⍠1⊢folder,'/*'

 config←##.Cider.ReadProjectConfigFile tempDir
 config.CIDER.dependencies.nuget←'MyNugetPkgs'
 config ##.Cider.WriteProjectConfigFile tempDir

 parms←##.Cider.CreateOpenParms''
 parms.folder←tempDir
 parms.noPkgLoad←0
 parms.batch←1
 parms.ignoreUserExec←1

 ##.Cider.C.YesOrNo_Answers←1 2⍴'ChangeCurrentDir@' 'n'

 (success log)←##.Cider.OpenProject parms
 →T.GoToTidyUp~success
 →T.GoToTidyUp∨/'ERROR'⍷log

 :Trap 11
     {}##.Cider.AddNuGetDependencies'Clock NodaTime,SMTP'tempDir
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     expected←'Invalid package name(s): "Clock NodaTime"'
     →T.GoToTidyUp expected≢qdmx.EM
 :EndTrap

 res←(~parms.batch)##.Cider.CloseProject tempDir
 →T.GoToTidyUp res≠1

 R←T._OK

∆TidyUp:
 ##.Cider.C.Cleanup
 res←∆LINK'Break' '#.TEMP'
 FilesAndDirs.RmDir tempDir,'/..'
⍝Done
