 R←Test_UC_047(stopFlag batchFlag);⎕TRAP;res;msg;rc;folder;parms;tempDir;success;log;stop;expected;alias
⍝ Exercise ]Cider.AddTatinDependencies without loading; uses a projet alias
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 :If ~IsDotNetAvailable
     R←T._NotApplicable ⋄ →0
 :EndIf

 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS''
 folder←∆GetTestPath⊃⎕SI
 tempDir←∆GetFolderInTempDir⊃⎕SI
 tempDir ⎕NCOPY⍠1⊢folder,'/*'
 alias←'MyAliias_',⊃⎕SI
 res←⎕SE.Cider.AddAlias tempDir alias
 Assert 0=≢res

 parms←##.Cider.CreateOpenParms''
 parms.folder←∊⎕NPARTS tempDir
 parms.noPkgLoad←0
 parms.batch←1
 parms.ignoreUserExec←1

 ##.Cider.C.AddAutomation'YesOrNo' 'ChangeCurrentDir@' 'n'
 (success log)←##.Cider.OpenProject parms
 →T.GoToTidyUp~success
 →T.GoToTidyUp∨/'ERROR'⍷log

 stop←0
 ##.Cider.C.AddAutomation'YesOrNo' 'AddTatinDeps@' 'y'
 ##.Cider.C.AddAutomation'YesOrNo' 'LoadTatinDependenciesAfterAdding@' 'n'
 (rc msg res)←stop ∆UCMD'AddTatinDependencies FilesAndDirs,APLTreeUtils2 [',alias,']'
 →T.GoToTidyUp rc≠0
 →T.GoToTidyUp 0≠≢msg
 expected←'2 Tatin dependencies added:' 'aplteam-FilesAndDirs-' 'aplteam-APLTreeUtils2-'
 →T.GoToTidyUp~∧/res∘{∨/∨/⍵⍷⍕⍺}¨expected

 R←T._OK

∆TidyUp:
 ##.Cider.C.Cleanup
 {}∆LINK'Break' '#.TEMP'
 FilesAndDirs.RmDir tempDir,'/..'
 ⎕SE.Cider.DropAlias alias
⍝Done
