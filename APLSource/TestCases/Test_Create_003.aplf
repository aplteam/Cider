 R←Test_Create_003(stopFlag batchFlag);⎕TRAP;tempDir;list;res;stop;msg;rc;ref
⍝ Attempts to create and then open a project with a non-empty namespace and a non-empty folder - must fail
⍝ A config file is copied over in order to avoid user interaction
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 tempDir←∆GetFolderInTempDir⊃⎕SI
 3 ⎕MKDIR tempDir,'/APLSource'
 'Foo'#.⎕NS''
 #.Foo.⎕FX'r←TheAnswerIs' 'r←42'
 (⊂⎕NR'#.Foo.TheAnswerIs')⎕NPUT tempDir,'/APLSource/TheAnswerIs.aplf'
 (tempDir,'/cider.config')⎕NCOPY(##.CiderConfig.HOME,'/TestData/cider.config.Foo')

 ref←∆GetRefToCommTools
 ref.AddAutomation'YesOrNo' 'ChangeCurrentDir@' 'n'
 stop←0
 (rc msg res)←stop ∆UCMD'CreateProject "',tempDir,'" -batch -acceptConfig -noEdit -ignoreUserExec'

 →T.GoToTidyUp 0=rc
 →T.GoToTidyUp~∨/'Both the target namespace and the source folder are not empty'⍷msg

 R←T._OK

∆TidyUp:
 ref.Cleanup
 {}⎕SE.Link.Break'#.Foo'
 rc←⊃FilesAndDirs.RmDir tempDir,'/..'
 Assert rc∧.=0
 #.⎕EX'Foo'
⍝Done
