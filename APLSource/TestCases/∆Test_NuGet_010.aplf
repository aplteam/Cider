 R←∆Test_NuGet_010(stopFlag batchFlag);⎕TRAP;folder;parms;res;log;cals;success;tempDir
⍝ Open a project that has already two NuGet dependencies
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS''
 folder←(∆GetTestPath⊃⎕SI),'/'
 tempDir←∆TEMP_PATH
 tempDir ⎕NCOPY⍠1⊢folder,'/*'

 parms←##.Cider.CreateOpenParms''
 parms.folder←tempDir
 parms.noPkgLoad←0
 parms.batch←1
 parms.ignoreUserExec←1

 ##.Cider.C.AddAutomation'YesOrNo' 'ChangeCurrentDir@' 'n'
 (success log)←##.Cider.OpenProject parms
 →T.GoToTidyUp~success
 →T.GoToTidyUp∨/'ERROR'⍷log
 →T.GoToTidyUp~(⊂'UtcNow')∊#.TEMP.Clock.⎕NL-2
 →T.GoToTidyUp~∧/'Julian' 'Gregorian'∊⍕¨#.TEMP.NodaTime.CalendarSystem.⎕NL-2

 res←(~parms.batch)##.Cider.CloseProject tempDir
 →T.GoToTidyUp res≠1

 R←T._OK

∆TidyUp:
 ##.Cider.C.Cleanup
 res←∆LINK'Break' '#.TEMP'
⍝Done
