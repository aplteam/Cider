 R←Test_UC_052(stopFlag batchFlag);⎕TRAP;folder;res;log;cals;success;tempDir;config;msg;rc;stop;batch;package;cfg;ref
⍝ Open project, add a Tatin package to a non-default folder, load, call, list, close, re-open, close
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS''
 folder←∆GetTestPath⊃⎕SI
 tempDir←∆GetFolderInTempDir⊃⎕SI
 tempDir ⎕NCOPY⍠1⊢folder,'/*'

 ref←∆GetRefToCommTools
 ref.AddAutomation'YesOrNo' 'ChangeCurrentDir@' 'n'
 stop←0
 (rc msg res)←stop ∆UCMD'OpenProject ',tempDir,' -projectSpace=TEMP -batch -ignoreUserExec'
 Assert rc=0

 cfg←##.Cider.ReadProjectConfigFile tempDir
 cfg.CIDER.dependencies.tatin←'tatin-packages'
 cfg.CIDER.dependencies_dev.tatin←'tatin-packages_dev'
 cfg ##.Cider.WriteProjectConfigFile tempDir

 package←##.CiderConfig.HOME,'/TestData/APLTreeUtils2/aplteam-APLTreeUtils2-1.2.0.zip,'
 ref.AddAutomation'YesOrNo' 'AddTatinDeps@' 'y'
 ref.AddAutomation'YesOrNo' 'LoadTatinDependenciesAfterAdding@' 'y'
 (rc msg res)←stop ∆UCMD'AddTatinDependencies ',package,' ',tempDir,' -development'
 Assert rc=0
 →T.GoToTidyUp'aplteam-APLTreeUtils2-1.2.0'≢' '~⍨2⊃,res

 (rc msg res)←stop ∆UCMD'ListTatinDependencies ',tempDir
 Assert rc=0
 →T.GoToTidyUp 3≠≢res   ⍝ One data row, two for pimping

 ref.AddAutomation'YesOrNo' 'ClearWS@' 'n'
 (rc msg res)←∆UCMD'CloseProject ',tempDir,' -fast'
 Assert rc=0
 →T.GoToTidyUp'Number of projects closed: 1'≢res

 #.TEMP.(⎕EX ⎕NL-⍳9)

 (rc msg res)←stop ∆UCMD'OpenProject ',tempDir,' -projectSpace=TEMP -batch -ignoreUserExec'
 Assert rc=0
 →T.GoToTidyUp'Project successfully opened and established in: #.TEMP'≢res~⎕TC
 →T.GoToTidyUp~(⊂#.TEMP.APLTreeUtils2.GetOperatingSystem ⍬)∊'Win' 'Mac' 'Lin'

 (rc msg res)←∆UCMD'CloseProject ',tempDir,' -fast'
 Assert rc=0

 R←T._OK

∆TidyUp:
 ref.Cleanup
 res←∆LINK'Break' '#.TEMP'
 FilesAndDirs.RmDir tempDir,'/..'   ⍝ Does not work: the .NET DLL blocks this; there is no way around this at the time of writing (October 2023)
⍝Done
