 R←Test_Open_040(stopFlag batchFlag);⎕TRAP;parms;PW;PP;FR;success;log
⍝ Open a project with additional and in one case invalid system variables (SYSVARS)
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 'TEMP'#.⎕NS''

 (PW PP FR)←⎕PW ⎕PP ⎕FR
 parms←##.Cider.CreateOpenParms ⍬
 parms.folder←∆GetTestPath⊃⎕SI
 parms.parent←'#.TEMP'
 parms.watch←'ns'
 parms.ignoreUserExec←1
 parms.batch←1
 ##.Cider.C.AddAutomation'YesOrNo' 'ChangeCurrentDir@' 'n'
 (success log)←##.Cider.OpenProject parms
 Assert success
 →T.GoToTidyUp∨/'ERROR'⍷log

 →T.GoToTidyUp 101≠#.TEMP.Foo.⎕PW
 →T.GoToTidyUp 10≠#.TEMP.Foo.⎕PP
 →T.GoToTidyUp 1287≠#.TEMP.Foo.⎕FR

 R←T._OK

∆TidyUp:
 ##.Cider.C.Cleanup
 (⎕PW ⎕PP ⎕FR)←PW PP FR
 {}(~parms.batch)⎕SE.Cider.CloseProject'#.TEMP.Foo'
 #.⎕EX'TEMP'
 #.⎕EX parms.folder
⍝Done
