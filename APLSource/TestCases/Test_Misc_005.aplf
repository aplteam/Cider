 r←Test_Misc_005(stopFlag batchFlag);⎕TRAP;dir;parms;rc;success;log;res
⍝ Exercise updating outdated Tatin packages
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 dir←FilesAndDirs.GetTempPath,'Cider/',⊃⎕SI
 FilesAndDirs.RmDir dir                         ⍝ Paranoia...
 3 ⎕MKDIR dir
 dir ⎕NCOPY⍠'Wildcard' 1⊣(∆GetTestPath⊃⎕SI),'/Project_Foo/*'
 Assert FilesAndDirs.IsDir dir,'/packages/aplteam-MarkAPL-11.0.0'
 Assert FilesAndDirs.IsDir dir,'/packages_dev/aplteam-Tester2-3.1.1'

 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS''

 parms←##.Cider.CreateOpenParms ⍬
 parms.folder←dir
 parms.projectSpace←'TEMP'
 parms.checkPackageVersions←2
 parms.watch←'ns'
 parms.ignoreUserExec←1
 parms.batch←1
 ##.Cider.C.AddAutomation'YesOrNo' 'ChangeCurrentDir@' 'n'
 (success log)←##.Cider.OpenProject parms
 Assert success
 →T.GoToTidyUp∨/'ERROR'⍷log

 →T.GoToTidyUp 0≠FilesAndDirs.IsDir dir,'/packages/aplteam-MarkAPL-11.0.0'
 →T.GoToTidyUp 1≠≢⊃FilesAndDirs.Dir2 dir,'/packages/aplteam-MarkAPL-11*'
 →T.GoToTidyUp 0≠FilesAndDirs.IsDir dir,'/packages_dev/aplteam-Tester2-3.1.1'
 →T.GoToTidyUp 0=≢⊃FilesAndDirs.Dir2 dir,'/packages_dev/aplteam-Tester2-3.*'

 r←T._OK

∆TidyUp:
 ##.Cider.C.Cleanup
 res←∆LINK'Break'#.TEMP
 Assert res ∆StartsWith'Unlinked:'
 rc←FilesAndDirs.RmDir dir
 Assert 0=+/∊rc
