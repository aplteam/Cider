 r←Test_UC_004(stopFlag batchFlag);⎕TRAP;tempDir;config;res;rc;ref;msg
⍝ Exercise the user command "CreateProject" with a path and a namespace name
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 tempDir←∆GetFolderInTempDir(⊃⎕SI),'_1'
 (tempDir,'/cider.config')⎕NCOPY(##.CiderConfig.HOME,'/TestData/cider.config.Foo')
 #.⎕SHADOW'Foo' ⋄ 'Foo'#.⎕NS''

 config←∆GetJSON5 tempDir,'/cider.config'
 (⊂⎕JSON⍠('Compact' 0)('Dialect' 'JSON5')⊣config)⎕NPUT(tempDir,'/cider.config')1

 ref←∆GetRefToCommTools
 ref.YesOrNo_Answers←1 2⍴'TargetNotEmpty@' 'y'
 ref.YesOrNo_Answers⍪←'OpenAsWell@' 'y'
 ref.YesOrNo_Answers⍪←'ChangeCurrentDir@' 'n'
 (rc msg res)←∆UCMD'CreateProject "',tempDir,'" #.Foo -noEdit -acceptConfig -ignoreUserExec -batch'
 Assert rc=0
 'You did not just press <enter>!'Assert∨/'Project successfully loaded and established in'⍷res
 →T.GoToTidyUp~(⊂'#.Foo')∊{⍵[;1]}##.Cider.ListOpenProjects 0

 r←T._OK

∆TidyUp:
 ref.Cleanup
 res←∆LINK'Break' '#.Foo'
 Assert res ∆StartsWith'Unlinked:'
 rc←FilesAndDirs.RmDir tempDir,'/..'
 Assert 0=+/∊rc
⍝Done
