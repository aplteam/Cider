 R←Test_OpenAndClose_002(stopFlag batchFlag);⎕TRAP;tempDir;sourceFolder;list;rc;ref;res;msg
⍝ Creates and opens a project and then closes it by path
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 tempDir←∆GetFolderInTempDir⊃⎕SI
 sourceFolder←tempDir,'/APLSource'
 'Create!'FilesAndDirs.CheckPath sourceFolder
 (tempDir,'/cider.config')⎕NCOPY(##.CiderConfig.HOME,'/TestData/cider.config.Foo')
 (⊂'r←Hello' 'r←''World''')FilesAndDirs.NPUT sourceFolder,'/hello.aplf'

 ref←∆GetRefToCommTools
 ref.AddAutomation'YesOrNo' 'ChangeCurrentDir@' 'n'
 (rc msg res)←∆UCMD'CreateProject "',tempDir,'" # -noEdit -acceptConfig -batch -ignoreUserExec'
 msg Assert 0=rc
 list←FilesAndDirs.ListFiles2 tempDir
 →T.GoToTidyUp 1≠≢list
 →T.GoToTidyUp ##.Cider.ConfigFilename≢⊃,/1↓⎕NPARTS 1⊃list

 R←T._OK

∆TidyUp:
 ref.Cleanup
 {}⎕SE.Link.Break #.Foo
 #.⎕EX'Foo'
 rc←FilesAndDirs.RmDir tempDir,'/..'
 Assert 0=+/∊rc
⍝Done
