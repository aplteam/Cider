 R←Test_Create_002(stopFlag batchFlag);⎕TRAP;tempDir;list;res;stop;msg;rc;qdmx;expected;ref
⍝ Attempts to create and then open a project with a non-empty namespace and an empty folder with
⍝ A config file is copied over in order to avoid user interaction
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 tempDir←∆GetFolderInTempDir⊃⎕SI
 'Foo'#.⎕NS''
 #.Foo.⎕FX'r←TheAnswerIs' 'r←42'
 (tempDir,'/cider.config')⎕NCOPY(##.CiderConfig.HOME,'/TestData/cider.config.Foo')

 ref←∆GetRefToCommTools
 ref.AddAutomation'YesOrNo' 'ChangeCurrentDir@' 'n'
 stop←0
 (rc msg res)←stop ∆UCMD'CreateProject "',tempDir,'" -acceptConfig -noEdit -batch -ignoreUserExec'
 Assert 0=rc
 Assert 0=≢msg

 →T.GoToTidyUp 2≠≢#.Foo.⎕NL⍳16  ⍝ TheAnswerIs plus CiderConfig = 2
 expected←1+⎕NEXISTS tempDir,'/APLSource/.linkconfig'  ⍝ introduced with Link 4.0
 →T.GoToTidyUp expected≠≢⊃FilesAndDirs.Dir2 tempDir,'/APLSource/'

 R←T._OK

∆TidyUp:
 ref.Cleanup
 {}⎕SE.Link.Break'#.Foo'
 rc←⊃FilesAndDirs.RmDir tempDir,'/..'
 Assert rc∧.=0
 #.⎕EX'Foo'
⍝Done
