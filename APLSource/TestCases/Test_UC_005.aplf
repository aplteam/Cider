 r←Test_UC_005(stopFlag batchFlag);⎕TRAP;tempDir;res;rc;config;msg;ref
⍝ Exercise the user command "CreateProject" with a path and a namespace name but without the flag
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 tempDir←∆GetFolderInTempDir(⊃⎕SI),'_1'
 (tempDir,'/cider.config')⎕NCOPY(##.CiderConfig.HOME,'/TestData/cider.config.Foo')

 #.⎕SHADOW'Foo' ⋄ 'Foo'#.⎕NS''
 #.Foo.⎕FX'r←TheAnswerIs' 'r←42'

 config←∆GetJSON5 tempDir,'/cider.config'
 (⊂⎕JSON⍠('Compact' 0)('Dialect' 'JSON5')⊣config)⎕NPUT(tempDir,'/cider.config')1

 ref←∆GetRefToCommTools
 ref.AddAutomation'YesOrNo' 'ChangeCurrentDir@' 'n'
 (rc msg res)←∆UCMD'CreateProject "',tempDir,'" #.Foo -noEdit -acceptConfig -batch -ignoreUserExec'
 Assert rc=0

 →T.GoToTidyUp~∨/∨/¨'TheAnswerIs.aplf'∘⍷¨⊃('recurse' 1)FilesAndDirs.Dir2 tempDir,'/'
 →T.GoToTidyUp 42≢#.Foo.TheAnswerIs

 r←T._OK

∆TidyUp:
 ref.Cleanup
 res←∆LINK'Break' '#.Foo'
 Assert res ∆StartsWith'Unlinked:'
 rc←FilesAndDirs.RmDir tempDir,'/..'
 Assert 0=+/∊rc
⍝Done
