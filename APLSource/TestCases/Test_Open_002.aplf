 R←Test_Open_002(stopFlag batchFlag);⎕TRAP;parms;folder;r;res;rc;success;log
⍝ Open a simple project but specify a different parent space including a sub namespace
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''                 ⍝ This is defined by the config file but overwritten by the argument
 '___Goo___'⎕SE.⎕NS''
 folder←∆GetTestPath⊃⎕SI

 parms←⎕NS''
 parms.folder←folder
 parms.parent←'⎕SE.___Goo___'
 parms.watch←'ns'
 parms.ignoreUserExec←1
 parms.batch←1
 ##.Cider.C.AddAutomation'YesOrNo' 'ChangeCurrentDir@' 'n'
 (success log)←##.Cider.OpenProject parms
 →T.GoToTidyUp~success
 →T.GoToTidyUp∨/'ERROR'⍷log
 →T.GoToTidyUp 0≠≢#.TEMP.⎕NL⍳16
 →T.GoToTidyUp 9≠⎕SE.___Goo___.⎕NC'Foo'
 →T.GoToTidyUp'World'≢⎕SE.___Goo___.Foo.Hello

 R←T._OK

∆TidyUp:
 ##.Cider.C.Cleanup
 res←∆LINK'Break' '⎕se.___Goo___.Foo'
 Assert res ∆StartsWith'Unlinked:'
 ⎕SE.⎕EX'___Goo___'
 #.⎕EX'TEMP'
⍝Done
