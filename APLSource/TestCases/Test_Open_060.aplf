 R←Test_Open_060(stopFlag batchFlag);⎕TRAP;parms;defs;log;success
⍝ Open a project with an alias "."
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 R←T._Failed

 'TEMP'#.⎕NS''

 parms←##.Cider.CreateOpenParms ⍬
 parms.folder←∆GetTestPath⊃⎕SI
 parms.parent←'#.TEMP'
 parms.alias←'.'
 parms.watch←'ns'
 parms.ignoreUserExec←1
 parms.batch←1
 ##.Cider.C.YesOrNo_Answers←1 2⍴'ChangeCurrentDir@' 'n'
 (success log)←##.Cider.OpenProject parms
 Assert success
 →T.GoToTidyUp∨/'ERROR'⍷log
 defs←##.Cider.GetCiderAliasFileContent ⍬
 →T.GoToTidyUp~(⊂⎕C⊃⎕SI)∊defs[;1]

 R←T._OK

∆TidyUp:
 ##.Cider.C.Cleanup
 {}(~parms.batch)⎕SE.Cider.CloseProject'#.TEMP.Foo'
 #.⎕EX'TEMP'
 defs←(defs[;1]≢¨⊂⎕C⊃⎕SI)⌿defs
 (⊂⊃¨{⍺,'=',⍵}/¨↓defs)⎕NPUT ##.Cider.GetCiderAliasFilename 1
⍝Done
