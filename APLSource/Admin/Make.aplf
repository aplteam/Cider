 {r}←{refToMakeHelpers}Make flag;M;C;path;zipFilename;res;parms;cfg;noOf;answer;q;targetFolder
⍝ "Make"s this projects.
⍝ You may pass a ref pointing to `MakeHelpers` as `⍺`.
⍝ If you don't, the function checks whether it's available in `⎕SE`.
⍝ If it is, the user is asked whether she wants to use that.
⍝ If she refuses or if it's not available in `⎕SE`, then the function
⍝ attempts to load the package into `⎕SE` with ]Tatin.LoadPackages.
⍝ Returns 1 in case a new version was created and 0 otherwise.
 r←0
 :If flag
     :Section  ⍝ Establish ref to "MakeHelpers"
         :If 0=⎕NC'refToMakeHelpers'
             :If 9=⎕SE.⎕NC'MakeHelpers'
                 flag←¯1
                 :Repeat
                     answer←⍞,0/⍞←q←'Do you want to use "MakeHelpers" ',⎕SE.MakeHelpers.Version,' in ⎕SE (n=re-load) (Y/n)?'
                     answer←(≢q)↓answer
                     :If 0=≢answer
                     :OrIf 'Yy'∊⍨¯1↑answer
                         flag←1
                     :ElseIf 'Nn'∊⍨¯1↑answer
                         flag←0
                     :EndIf
                 :Until flag∊0 1
                 :If flag
                     M←⎕SE.MakeHelpers
                 :Else
                     noOf←⎕SE.Tatin.LoadPackages'MakeHelpers'⎕SE
                     'Could not load the package "MakeHelpers"'⎕SIGNAL 6/⍨0=noOf
                     M←⎕SE.MakeHelpers
                 :EndIf
             :Else
                 noOf←⎕SE.Tatin.LoadPackages'MakeHelpers'⎕SE
                 'Could not load the package "MakeHelpers"'⎕SIGNAL 6/⍨0=noOf
                 M←⎕SE.MakeHelpers
             :EndIf
         :Else
             M←refToMakeHelpers
         :EndIf
     :EndSection
     ⎕←'Using "MakeHelpers" version ',⎕SE.MakeHelpers.Version
     C←##.CiderConfig
     ⎕←'' ⋄ 0 0⍴⍞,0/⍞←'Pause sync services like Dropbox, Google Drive etc. in order to avoid FILE ACCESS errors. Press <enter> to continue '
     :If ⎕NULL≡cfg←M.EditPackageConfigFile ##.Cider.TatinVars
         ⎕←'Cancelled by user' ⋄ →0
     :EndIf
     path←C.HOME,'/',C.CIDER.distributionFolder
     targetFolder←##.Cider.GetCurrentAPLsFolderInDocuments,'SessionExtensions\CiderTatin\Cider'
     :If 0=⎕NEXISTS targetFolder,'\Cider_UC.dyalog'
         targetFolder←##.Cider.GetUserDocumentsFolder,'Dyalog APL Files\SessionExtensions\CiderTatin\Cider'
     :AndIf 0=⎕NEXISTS targetFolder,'\Cider_UC.dyalog'
     :AndIf ~1 M.YesOrNo'Could not find the user command script in the user''s home folder anywhere - continue?'
         :Return
     :EndIf
     M.FetchLaterUserCommand cfg C targetFolder
     noOf←⎕SE.Tatin.CreateAPIfromCFG ##.Cider cfg
     'Tally of the API is unexpected'Assert 30=noOf
     M.RecreateFolder path
     {}⎕SE.Tatin.CreateAPIfromCFG ##.Cider cfg
     parms←⎕SE.Tatin.CreateBuildParms C.HOME
     parms.targetPath←C.CIDER.distributionFolder
     parms.version←cfg.version
     zipFilename←⎕SE.Tatin.BuildPackage parms
     ⎕←'*** New version built successfully:',M.CR,'   ',zipFilename
     r←1
 :EndIf
⍝Done
