 r←{batch}InstallCider flag;C;aplVersion;selectFlag;os;installFolder;ind;ZIPs;zipFilename;q;res;if1;if2
⍝ Installs Cider into the appropriate folder, depending on the current OS.
⍝ The user will be asked whether to install into the version specific folder or agnostic folder.
⍝ `flag` is insurance against accidental calls.
⍝ `batch` defaults to 0. If it is 1 then no questions are asked.\\
⍝ If `batch` is 0 (the default) a message is returned via `r` reporting success, otherwise the
⍝ path to the installation folder is returned.
 r←''
 :If flag
     batch←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'batch'
     C←##.Cider
     aplVersion←# ⎕WG'APLVersion'
     selectFlag←0
     :Select os←C.APLTreeUtils2.GetOperatingSystem ⍬
     :Case 'Win'
         :If ~⎕NEXISTS installFolder←if1←##.Cider.GetCurrentAPLsFolderInDocuments,'\SessionExtensions\CiderTatin\Cider'
         :AndIf ~⎕NEXISTS installFolder←if2←##.Cider.GetUserDocumentsFolder,'Dyalog APL Files\SessionExtensions\CiderTatin\Cider'
             selectFlag←1
         :EndIf
     :Case 'Lin'
         :If ~⎕NEXISTS installFolder←if1←(2 ⎕NQ'.' 'GetEnvironment' 'HOME'),'/dyalog.files/SessionExtensions/CiderTatin/Cider'
         :AndIf ~⎕NEXISTS installFolder←if2←(2 ⎕NQ'.' 'GetEnvironment' 'HOME'),'/','dyalog.',('.'~⍨{⍵/⍨2>+\⍵='.'}2⊃aplVersion),'U',(¯2↑1⊃aplVersion),'.files/SessionExtensions/CiderTatin/Cider'
             selectFlag←1
         :EndIf
     :Case 'Mac'
         :If ~⎕NEXISTS installFolder←if1←(2 ⎕NQ'.' 'GetEnvironment' 'HOME'),'/dyalog.files/SessionExtensions/CiderTatin/Cider'
         :AndIf ~⎕NEXISTS installFolder←if2←(2 ⎕NQ'.' 'GetEnvironment' 'HOME'),'/','dyalog.',('.'~⍨{⍵/⍨2>+\⍵='.'}2⊃aplVersion),'U',(¯2↑1⊃aplVersion),'.files/SessionExtensions/CiderTatin/Cider'
             selectFlag←1
         :EndIf
     :Else
         0 Assert⍨'OS <',os,'> is not supported'
     :EndSelect
     :If selectFlag
         ind←'Into which folder?'C.CommTools.Select'Version specific' 'Version agnostic'
         :If 0=≢ind
             ⎕←'Cancelled by user' ⋄ →0
         :ElseIf ind=1
             installFolder←if1
         :ElseIf ind=2
             installFolder←if2
         :Else
             ∘∘∘  ⍝ Huh?!
         :EndIf
     :EndIf
     ZIPs←C.FilesAndDirs.ListFiles2 ##.CiderConfig.HOME,'/Dist/*.zip'
     ZIPs←{⍵/⍨'Installed-'{⍺∘≢¨(≢⍺)↑¨{2⊃⎕NPARTS ⍵}¨⍵}⍵}ZIPs
     Assert 1=≢ZIPs
     zipFilename←⊃ZIPs
     :If batch
         q←'Install:',(⎕UCS 13),('  ',zipFilename),(⎕UCS 13),'into:',(⎕UCS 13),'  ',installFolder,' ?'
     :OrIf 1 C.CommTools.YesOrNo q
         {}##.Cider.FilesAndDirs.RmDir installFolder
         3 ⎕MKDIR installFolder
         res←⎕SE.Tatin.InstallPackages(⊃ZIPs)installFolder
         Assert 1=≢res
         Assert(1⊃res)≡(2⊃⎕NPARTS zipFilename)
         :If batch
             r←installFolder
         :Else
             ⎕SE.SALTUtils.ResetUCMDcache ¯1
             r←'*** Cider installed in ',installFolder
         :EndIf
     :EndIf
 :EndIf
