 {r}←CheckForDropboxConflicts path;dmx;list;msg;found;dropbox;parms
⍝ Checks whether there are any Dropbox files indicating a conflict, and if so print them to the session.
⍝ Returns 0 in case there are not any such conflict files, otherwise the number of files found.
 r←found←0
 msg←''
 :If 0<≢path~' '
     :If ⍬≡dropbox←GetGlobalDropboxConflicts
         dropbox←⎕NEXISTS GetUserHomPath,'\Dropbox'
     :EndIf
 :AndIf dropbox
     msg,←⊂'Checking for Dropbox conflicts: ',path,' ...'
     parms←⎕NS''
     parms.recurse←2
     r←≢list←parms F.ListFiles2 path,'/*conflicted copy*'
     :If 0<r
         found←1
         msg,←⊂'  ********** The project has conflicting files! ************'
         msg,←⊂'  *** PATH: ',path
         msg,←'  '∘,¨(1+≢path)↓¨list
         msg,←⊂'  **********************************************************'
     :Else
         msg,←⊂'  No Dropbox conflicts found'
     :EndIf
 :EndIf
 :If found
     ⎕←↑msg
 :ElseIf 0<≢msg
     :If GetGlobalVerbose
         ⎕←↑msg
     :EndIf
 :EndIf
