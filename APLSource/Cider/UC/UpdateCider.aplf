 r←UpdateCider args;allVersions;thisVersion;ind;noOf;targetFolder;tempFolder;res;q;folder;flag;version;pkg
 r←''
 allVersions←,⎕SE.Tatin.ListVersions'[Tatin]aplteam-Cider'
 :If 0≢version←args._1
     ('Is not a valid Cider version: ',version)Assert(⊂version)∊{⍵↑⍨1+-(⌽⍵)⍳'-'}¨allVersions
     q←'UpdateCider@Are you sure that you want to update Cider ',P.Version,' to version ',version,' ?'
     pkg←'[tatin]aplteam-Cider-',version
 :Else
     thisVersion←'aplteam-Cider-',{⍵↑⍨¯1+⍵⍳'+'}P.Version
     ind←allVersions⍳⊂thisVersion
     noOf←≢allVersions
     :If ind=noOf
         r←'There is no later version available than ',P.Version ⋄ →0
     :EndIf
     q←'UpdateCider@Are you sure that you want to update Cider ',P.Version,' to version ',({1↓⍵/⍨2≤+\⍵='-'}noOf⊃allVersions),' ?'
     pkg←'[tatin]aplteam-Cider'
 :EndIf
 :If flag←1 P.##.C.YesOrNo q
     tempFolder←P.##.F.GetTempSubDir'Cider'
     res←⎕SE.Tatin.InstallPackages pkg tempFolder
     targetFolder←1 P.GetProgramFilesFolder'/SessionExtensions/CiderTatin/Cider'
     :If 0=⎕NEXISTS targetFolder    ⍝ If there is no version-specific folder...
             ⍝ ... we check for a version-agnostic folder:
         targetFolder←0 P.GetProgramFilesFolder'/SessionExtensions/CiderTatin/Cider'
     :EndIf
     {}P.##.F.RmDirByForce targetFolder
     3 ⎕MKDIR targetFolder
     targetFolder ⎕NMOVE⍠1⊣tempFolder,'/*'
     ⎕←'Cider was successfully updated to version ',({1↓⍵/⍨2≤+\⍵='-'}⊃res),' in ',targetFolder
     ⎕←'The workspace has not been updated, please restart APL.'
     {}P.##.F.RmDirByForce tempFolder
     folder←P.GetMyUCMDsFolder,'/Cider'
     :If P.##.F.IsDir folder
         q←'RemoveCiderFromMyUCMDs@There is a folder Cider/ in ',P.GetMyUCMDsFolder,'/',⎕UCS 13
         q,←'Shall this folder be removed?'
     :AndIf 1 P.##.C.YesOrNo q
         {}P.##.F.RmDirByForce folder
     :EndIf
     folder←1 P.GetProgramFilesFolder'/CiderTatin/Cider'
     :If P.##.F.IsDir folder
         q←'RemoveCiderFromProgramFiles@There is a folder Cider/ in ',('expand'P.##.F.NormalizePath folder,'\..'),'/',⎕UCS 13
         q,←'Shall this folder be removed?'
     :AndIf 1 P.##.C.YesOrNo q
         {}P.##.F.RmDirByForce folder
     :EndIf
 :Else
     r←'Cancelled by user'
 :EndIf
