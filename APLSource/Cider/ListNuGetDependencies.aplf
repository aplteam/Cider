 r←ListNuGetDependencies projectFolder;NuGet;cfg;nuGetFolder;list;list_id;r_id;b;folder
 r←0 2⍴''
 :If 0<≢NuGet←FindNuGetAPI''
     :If IsAlias projectFolder
         projectFolder←GetFolderFromAlias2 projectFolder
     :EndIf
     'No Cider config file found'Assert F.Exists projectFolder,'/cider.config'
     cfg←ReadProjectConfigFile projectFolder
     :If 0<≢nuGetFolder←⊃GetNuGetDependencyFolders cfg
         nuGetFolder←{~'='∊2⊃⎕NPARTS ⍵:⍵ ⋄ ⊃{⍺,{⍵↑⍨¯1+⍵⍳'='}⍵}/⎕NPARTS ⍵}nuGetFolder
         folder←projectFolder,'/',nuGetFolder
     :AndIf ⎕NEXISTS folder
     :AndIf 0<≢F.ListFiles folder,'/*.csproj'
         r←↑NuGet.Packages folder
     :EndIf
     r[;1]←GetRealNuGetNames folder(r[;1])
 :EndIf
