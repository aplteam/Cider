 report←report CheckTatinFoldersForLaterVersions(parms folders);folder;folder_;qdmx;msg
⍝ Loop through all Tatin install folders
 msg←''
 :For folder :In folders
     folder_←⊃'='(≠⊆⊢)folder
     :If F.Exists parms.folder,'/',folder_
         p'Checking Tatin packages in <',(AddSlash folder_),'> ...'
         folder_←parms.folder,'/',folder_
         :If 0<≢⊃0 ⎕NINFO⍠('Wildcard' 1)⊣folder_,'/*'
             :Trap 998
                 report,←⊂parms.major ⎕SE.Tatin.CheckForLaterVersion folder_
             :Else
                 qdmx←⎕DMX
                 qdmx.EM Assert~parms.batch
                 msg←''
                 msg,←⊂'ERROR: Checking for later versions of Tatin packages failed:'
                 msg,←⊂' ',qdmx.EM
             :EndTrap
         :Else
             msg←'Installation folder <',parms.folder,'/',folder,'> is empty'
             msg Assert~parms.batch
         :EndIf
     :EndIf
 :EndFor
 1 PrintToSession msg
⍝Done
