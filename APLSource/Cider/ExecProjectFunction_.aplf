{r}←x ExecProjectFunction_ init;fns;config;projectSpace;qdmx;valence;parms
 r←⍬
 (projectSpace config parms)←x
 :If 3=projectSpace.⎕NC init
     p'Executing the project''s initialising function "',(init~' '),'"...'
     valence←1 2⊃projectSpace.⎕AT init
     qdmx←⍬
     :Select valence
     :Case 0                ⍝ Niladic
         :Trap 0
             projectSpace.⍎init
         :Else
             qdmx←⎕DMX
         :EndTrap
     :Case 1                ⍝ Monadic
         fns←projectSpace.⍎init
         :Trap 0
             fns config
         :Else
             qdmx←⎕DMX
         :EndTrap
     :CaseList 2 ¯2               ⍝ Ambivalent, dyadic
         fns←projectSpace.⍎init
         :Trap 0
             parms fns config
         :Else
             qdmx←⎕DMX
         :EndTrap
     :EndSelect
     :If ⍬≢qdmx
         (1 parms.batch)p'>>> Executing "init" crashed: ',qdmx.Message{0=≡⍺:⍵ ⋄ ⍵,'; ',⍺}qdmx.EM
     :EndIf
 :Else
     (1 parms.batch)p'>>> Could not execute "',init,'": function not found; check config file!'
 :EndIf
