 r←ListTatinDependenciesFull tatinFolder;dependencyFilename;filenames;buff
⍝ `y` must point to a project, either as alias ([]) or as a project folder.
 ('"',tatinFolder,'" does not point to an existing folder')Assert ⎕NEXISTS tatinFolder
 dependencyFilename←tatinFolder,'/apl-dependencies.txt'
 ('Not found: "',dependencyFilename,'"')Assert F.Exists dependencyFilename
 r←0,¨⊂¨GetDependencies dependencyFilename
 filenames←{tatinFolder,'/',⍵,'/apl-dependencies.txt'}¨2⊃¨r
 buff←filenames ListTatinDependenciesFull_¨⊂1 tatinFolder
 r←r{0=≢⍵:⊂⍺ ⋄ ¯4=≡⍵:(⊂⍺),⊃,/⍵ ⋄ (⊂⍺),⍵}¨buff
 r←(0<≢¨r)/r
 r←⊃⍪/↑¨,¨r
⍝Done
