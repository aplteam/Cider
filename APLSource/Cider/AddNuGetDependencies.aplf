 r←AddNuGetDependencies (packages path);NuGet;ns;config;hasdeps;nuget_dir
⍝ Either add one or more NuGet packages

 NuGet←⎕SE.NuGet
 ns←path{3::0 ⋄ ⊃⍵[⍵[;2]⍳⊂⍺;1]}ListOpenProjects 0 ⍝ namespace project loaded into
 config←Get_JSON5 configfile←path,'/cider.config'
 hasdeps←0≠⎕NC'config.CIDER.dependencies.nuget'

 :If hasdeps∧0=⎕NC 'config.CIDER.dependencies.nuget'
     config.CIDER.dependencies.nuget←'nuget-packages'
     config Put_JSON5 configfile
 :EndIf

 nuget_dir←path,'/',{0::⍵ ⋄ config.CIDER.dependencies.nuget}'nuget-packages'

 r←NuGet.Setup nuget_dir
 →(∨/'error:'⍷⎕C r)⍴0

 r←NuGet.Add nuget_dir packages
 →(∨/'error:'⍷⎕C r)⍴0

 :If ns≢0 ⍝ We have a project namespace
     :If 2=⎕NC 'ns'
         ns←⍎ns
     :EndIf
     'dotnet'ns.⎕NS''
     ns.dotnet.⎕USING←NuGet.Using nuget_dir
     r←'NuGet dependencies established in ',(⍕ns),'.dotnet'
 :EndIf
