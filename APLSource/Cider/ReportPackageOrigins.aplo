 msg←(reportFns ReportPackageOrigins)folder;data;noOf
⍝ Report where the package got installed from in case they did NOT come from https://tatin.dev/
 msg←''
 data←Get_JSON5 folder,'/apl-buildlist.json'
 :If ~∧/'https://tatin.dev/'∘≡¨data.url
     noOf←{≢⍵}⌸data.url
     :If 1=≢noOf
         msg←'All packages (',(⍕noOf),') were originally installed from ',⊃data.url
         {}reportFns↓⎕FMT msg   ⍝ Report to ⎕SE only if installed from more than one URL
     :Else
         msg←1 2⍴'Packages were originally installed from:' ''
         msg⍪←↑noOf{⍵(('(',⍕⍺),' package',((1<⍺)/'s'),')')}¨∪data.url
         {}reportFns↓⎕FMT msg   ⍝ Report to ⎕SE only if installed from more than one URL
     :EndIf
 :EndIf
